// radius, height in nanometers
R=RADIUS/1000.;
h=HEIGHT/1000.;


l=ELEMSIZ/1000.;

// Circle centers
Point(1) = { 0,  0,  h/2.,l};
Point(2) = { 0,  0, -h/2.,l};

// Upper circle arc limits
Point(3) = { R,  0,  h/2.,l};
Point(4) = { 0,  R,  h/2.,l};
Point(5) = {-R,  0,  h/2.,l};
Point(6) = { 0, -R,  h/2.,l};

// Upper circle
Circle(1) = {3, 1, 4};
Circle(2) = {4, 1, 5};
Circle(3) = {5, 1, 6};
Circle(4) = {6, 1, 3};

// Lower circle arc limits
Point(7)  = { R,  0, -h/2.,l};
Point(8)  = { 0,  R, -h/2.,l};
Point(9)  = {-R,  0, -h/2.,l};
Point(10) = { 0, -R, -h/2.,l};

// Lower circle
Circle(5) = { 7, 2,  8};
Circle(6) = { 8, 2,  9};
Circle(7) = { 9, 2, 10};
Circle(8) = {10, 2,  7};

Line( 9) = {3,7};
Line(10) = {4,8};
Line(11) = {5,9};
Line(12) = {6,10};

Line Loop(13) = {1, 2, 3, 4};
Line Loop(14) = {5, 6, 7, 8};

Line Loop(15) = {1, 10, -5,  -9};
Line Loop(16) = {2, 11, -6, -10};
Line Loop(17) = {3, 12, -7, -11};
Line Loop(18) = {4,  9, -8, -12};

Ruled Surface(19) = {13};
Ruled Surface(20) = {14};
Ruled Surface(21) = {15};
Ruled Surface(22) = {16};
Ruled Surface(23) = {17};
Ruled Surface(24) = {18};



Physical Surface(1) = {19,20,21,22,23,24};




